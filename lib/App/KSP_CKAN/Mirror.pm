package App::KSP_CKAN::Mirror;

use v5.010;
use strict;
use warnings;
use autodie;
use Method::Signatures 20140224;
use File::chdir;
use Carp qw( croak );
use File::Temp qw(tempdir);
use File::Path qw(remove_tree);
use App::KSP_CKAN::Tools::IA;
use App::KSP_CKAN::Tools::Http;
use App::KSP_CKAN::Metadata::Ckan;
use Moo;
use namespace::clean;

# ABSTRACT: MirrorKAN mirroring service

# VERSION: Generated by DZP::OurPkg:Version

=head1 SYNOPSIS

  use App::KSP_CKAN::Mirror;

  my $mirror = App::KSP_CKAN::Mirror->new(
    config => $config,
  );

=head1 DESCRIPTION

Mirroring abstraction to be used by other libraries.

=cut

my $Ref = sub {
  croak("auth isn't a 'App::KSP_CKAN::Tools::Config' object!") unless $_[0]->DOES("App::KSP_CKAN::Tools::Config");
};

has 'config'      => ( is => 'ro', required => 1, isa => $Ref );
has '_ia'         => ( is => 'ro', lazy => 1, builder => 1 );
has '_http'       => ( is => 'ro', lazy => 1, builder => 1 );

method _build__ia {
  return App::KSP_CKAN::Tools::IA->new(config => $self->config);
}

method _build__http {
  return App::KSP_CKAN::Tools::Http->new();
}

method _tmp_dir {
  return File::Temp::tempdir();
}

method _clean_tmp_dir($tmp) {
  if ( -d $tmp ) {
    remove_tree($tmp);
  }
  return;
}

# TODO: What happens when we load an invalid file?
method _load_ckan($ckanfile) {
  $self->logdie("Ckan file does not exist at '$ckanfile'") unless (-f $ckanfile);
  return App::KSP_CKAN::Metadata::Ckan->new( file => $ckanfile );
}

# TODO: Could use some more logic here, maybe a while loop.
method _check_overload($tmp) {
  if ( $self->_ia->check_overload ) {
    # Let's clean ourselves up first.
    $self->_clean_tmp_dir($tmp);
    $self->logdie("The Internet Archive is overloaded, try again later");
  }
  return;
}

# TODO: A lot of this logic should be moved into the IA class. We could
#       very easily support multiple mirror backends. Potentially
#       convert IA to a Role, or make this class exntendable with a
#       required attribute of App::KSP_CKAN::Mirror::$Backend
method upload_ckan($ckanfile) {
  my $ckan = $self->_load_ckan($ckanfile);
  $self->logdie("Ckan '".$ckan->mirror_item."' cannot be mirrored") unless $ckan->can_mirror;
  my $tmp = $self->_tmp_dir;
  my $file = $tmp."/".$ckan->mirror_filename;
  $self->_http->mirror(
    url   => $ckan->download,
    path  => $file,
  );
  $self->_check_overload($tmp);
  
  my $result = $self->_ia->put_ckan(
    ckan => $ckan,
    file => $file,
  );
  
  if ( $result ) {
    $self->info($ckan->mirror_item." mirrored to the Internet Archive successfully");
    return 1;
  } else {
    $self->warn($ckan->mirror_item." failed to mirror to the Internet Archive");
  }
  $self->_clean_tmp_dir($tmp);
  return 0;
}

with('App::KSP_CKAN::Roles::Logger');

1;
