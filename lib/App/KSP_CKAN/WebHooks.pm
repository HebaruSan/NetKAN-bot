package App::KSP_CKAN::WebHooks;

use Dancer2 appname => "xKanHooks";
use App::KSP_CKAN::WebHooks::InflateNetKAN;
use AnyEvent::Util;
use Try::Tiny;
use File::Touch;

# ABSTRACT: Webhook Routes for ckan-webhooks

# VERSION: Generated by DZP::OurPkg:Version

post '/mirror' => sub { 
  info(request->body);

  status(204);
  return;
};

post '/inflate' => sub {
  my @identifiers;
  
  try {
    @identifiers = @{from_json(request->body)->{identifiers}};
  };

  if ($#identifiers == -1) {
    info("No identifiers received"); 
    send_error "An array of identifiers is required", 400;
  }

  fork_call {
    my $inflater = App::KSP_CKAN::WebHooks::InflateNetKAN->new();

    while (-e "/tmp/xKan_netkan.lock" ) {
      debug("Waiting for lock release");
      sleep 5;
    }
    
    # TODO: Do something better, this doesn't handle stale
    #       locks at all. Also if following requests come in
    #       at exactly 5 seconds apart we could still fork
    #       twice simultaneously.
    debug("Locking environment");
    touch("/tmp/xKan_netkan.lock");
    
    foreach my $netkan (@identifiers) {
      info("Inflating $netkan");
      $inflater->inflate($netkan);
      info("Completed $netkan");
    }

    return;
  } sub {
    debug("Unlocking environment");
    unlink("/tmp/xKan_netkan.lock");
    return;
  };

  status(204);
  return;
};

1;
