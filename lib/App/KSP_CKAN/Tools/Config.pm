package App::KSP_CKAN::Tools::Config;

use v5.010;
use strict;
use warnings;
use autodie;
use Method::Signatures 20140224;
use Config::Tiny;
use Carp qw( croak );
use Moo;
use namespace::clean;

# ABSTRACT: A lite wrapper around Config::Tiny

# VERSION: Generated by DZP::OurPkg:Version

=head1 SYNOPSIS

  use App::KSP_CKAN::Git;

  my $git = App::KSP_CKAN::Config->new(
    file => "/path/to/file",
  );

=head1 DESCRIPTION

Provides a config object for KSP-CKAN

=cut

has 'file'      => ( is => 'ro', default => sub { $ENV{HOME}."/.ksp-ckan" } );
has '_config'   => ( is => 'ro', lazy => 1, builder => 1 );
has 'CKAN_meta' => ( is => 'ro', lazy => 1, builder => 1 );
has 'NetKAN'    => ( is => 'ro', lazy => 1, builder => 1 );
has 'GH_token'  => ( is => 'ro', lazy => 1, builder => 1 );
has 'working'   => ( is => 'ro', lazy => 1, builder => 1 );

method _build__config {
  if ( ! -e $self->file ) {
    croak( "No config at ".$self->file );
  }
  return Config::Tiny->read( $self->file );
}

method _build_CKAN_meta {
  croak( "Missing 'CKAN_meta' from config" ) if ! $self->_config->{_}{'CKAN_meta'};
  return $self->_config->{_}{'CKAN_meta'};
}

method _build_NetKAN {
  croak( "Missing 'NetKAN' from config" ) if ! $self->_config->{_}{'NetKAN'};
  return $self->_config->{_}{'NetKAN'};
}

method _build_GH_token {
  if ( ! $self->_config->{_}{'GH_token'} ) {
    return 0;
  } else {
    return $self->_config->{_}{'GH_token'};
  }
}

method _build_working {
  if ( ! $self->_config->{_}{'working'} ) {
    return $ENV{HOME}."/CKAN-working";
  } else {
    return $self->_config->{_}{'working'};
  }
}

1;
